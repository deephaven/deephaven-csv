plugins {
    id 'java-library'
    id 'com.diffplug.spotless'
}

repositories {
    mavenCentral()
}

group = 'io.deephaven'
archivesBaseName = 'deephaven-csv-fast-double-parser-8'
version = project.property('version')

description = 'The Deephaven High-Performance CSV Parser - Fast Double extension (Java 8+)'

// Java 11+ required for building
if (!JavaVersion.current().isJava11Compatible()) {
    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(11)
        }
    }
}

// Java 8 compatible jar
tasks.named('compileJava', JavaCompile) {compile ->
    compile.options.release.set 8
}

dependencies {
    api rootProject

    // Requires Java 8+
    implementation 'ch.randelshofer:fastdoubleparser:0.2.0'

    testImplementation 'org.assertj:assertj-core:3.23.1'
    testImplementation(platform('org.junit:junit-bom:5.8.2'))
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

tasks.named('test') { test ->
    test.useJUnitPlatform()
}

spotless {
    java {
        eclipse().configFile("${rootDir}/style/eclipse-java-google-style.xml")
    }
}

apply plugin: 'io.deephaven.csv.java-publishing-conventions'
